apply plugin: 'java'
apply plugin: 'maven'

group = 'com.xebialabs.deployit'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.6
sourceEncoding = 'UTF-8'

configurations {
  // http://issues.gradle.org/browse/GRADLE-784
  provided
}

serverApiVersion = '3.5.1.2'
pluginApiVersion = '3.5.1.2'
trueZipVersion = '7.3.1'

dependencies {
  provided "com.xebialabs.deployit:server-plugin-api:${serverApiVersion}"
  provided "com.xebialabs.deployit.plugins:jee-plugin:${pluginApiVersion}"
  compile "com.xebialabs.deployit:single-file-importer:1.0-SNAPSHOT"
  compile "de.schlichtherle.truezip:truezip-file:${trueZipVersion}"
  runtime "de.schlichtherle.truezip:truezip-driver-zip:${trueZipVersion}"
  testCompile 'junit:junit:4.8.2'
  testCompile 'ch.qos.logback:logback-classic:0.9.24'
  testCompile 'com.qrmedia.commons:commons-lang:1.0.2'
}

repositories {
	mavenLocal()
}

sourceSets {
  main { compileClasspath += configurations.provided }
  
  // need the server-api and plugin-api to build *and* run the tests
  test { 
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
}

afterEvaluate { eachProject ->
  eachProject.tasks.withType(Test).each { testTask ->
    testTask.onlyIf { !project.hasProperty('skipTests') }
    testTask.afterSuite { descriptor, result ->
      if (descriptor.className != null) {
        println "${descriptor.className}: ${result.testCount} tests; failed: ${result.failedTestCount}; skipped: ${result.skippedTestCount}"
      } else if (descriptor.className == null && descriptor.parent == null) {
        println "Total: ${result.testCount} tests; failed: ${result.failedTestCount}; skipped: ${result.skippedTestCount}"
      }
    }
  }
}

task generatePom << {
  conf2ScopeMappings.addMapping(Integer.MAX_VALUE, configurations.provided, 
    Conf2ScopeMappingContainer.PROVIDED)
  pom { 
    project { 
      build { 
        plugins {
          plugin {
            artifactId 'maven-compiler-plugin'
            version '2.3'
            configuration {
              encoding = sourceEncoding 
              source = "${sourceCompatibility}".toString()
              target = "${sourceCompatibility}".toString()
            }
          }
          plugin {
            artifactId 'maven-resources-plugin'
            version '2.3'
            configuration {
              encoding = sourceEncoding
            }
          }
        } 
      } 
    } 
  }.writeTo('pom.xml')
}
